# $Id$
# Mainainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=diffutils
pkgver=3.6
pkgrel=1
pkgdesc='Utility programs used for creating patch files'
arch=('x86_64' 'ppc' 'ppc64')
url='https://www.gnu.org/software/diffutils'
license=('GPL3')
groups=('base')
depends=('glibc' 'bash')
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE') # Jim Meyering
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('07cf286672ced26fba54cd0313bdc071'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr gl_cv_func_getopt_gnu=yes
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
