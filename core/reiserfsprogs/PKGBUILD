# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=reiserfsprogs
pkgver=3.6.27
pkgrel=1
pkgdesc="Reiserfs utilities"
arch=('x86_64' 'ppc' 'ppc64')
url="http://www.kernel.org/"
license=('GPL')
groups=('base')
depends=('util-linux')
source=("https://www.kernel.org/pub/linux/kernel/people/jeffm/reiserfsprogs/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('90c139542725efc6da3a6b1709695395')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CFLAGS+=' -fgnu89-inline'
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
