#$Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libcap
pkgver=2.25
pkgrel=1
pkgdesc='POSIX 1003.1e capabilities'
arch=('x86_64' 'ppc' 'ppc64')
url='http://sites.google.com/site/fullycapable/'
license=('GPL2')
depends=('glibc' 'attr')
makedepends=('linux-api-headers')
validpgpkeys=('38A644698C69787344E954CE29EE848AE2CCF3F4') # Andrew G. Morgan <morgan@kernel.org>
source=(https://kernel.org/pub/linux/libs/security/linux-privs/libcap2/libcap-$pkgver.tar.{xz,sign}
        fix-build-with-gperf-3.1.patch)
md5sums=('6666b839e5d46c2ad33fc8aa2ceb5f77' 'SKIP' 'SKIP')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-build-with-gperf-3.1.patch
  sed -i "/SBINDIR/s#sbin#bin#" Make.Rules
}

build() {
  make -C $pkgname-$pkgver KERNEL_HEADERS=/usr/include
}

package() {
  cd $pkgname-$pkgver
  make prefix=/usr lib=/lib DESTDIR="$pkgdir" RAISE_SETFCAP=no install

  install -Dm644 pam_cap/capability.conf \
    "$pkgdir"/usr/share/doc/$pkgname/capability.conf.example
}
