# $Id: PKGBUILD 373039 2018-08-21 07:39:53Z felixonmars $ 
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgbase=python-sqlalchemy
pkgname=('python-sqlalchemy' 'python2-sqlalchemy')
pkgver=1.2.11
pkgrel=1
arch=('x86_64' 'ppc' 'ppc64') # python2 package contain .so
url="http://www.sqlalchemy.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-xdist' 'python2-pytest-xdist' 'python-mock' 'python2-mock')
source=("https://pypi.io/packages/source/S/SQLAlchemy/SQLAlchemy-$pkgver.tar.gz"{,.asc})
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1')
sha512sums=('dfe60cdf4923239d9ca16ee2ee8618ebe3fe2553c35d393f27c51aec16bcba3a6692e7f045e8206a41ad7cfbf71380d607cb27cbc9a6320faedbcd3a37517604'
            'SKIP')

prepare() {
  cp -a SQLAlchemy-$pkgver SQLAlchemy2-$pkgver
}

build() {
  cd "$srcdir"/SQLAlchemy-$pkgver
  python setup.py build

  cd "$srcdir"/SQLAlchemy2-$pkgver
  python2 setup.py build
}

check() {
  cd "$srcdir"/SQLAlchemy-$pkgver
  python setup.py pytest
 
  cd "$srcdir"/SQLAlchemy2-$pkgver  
  python2 setup.py pytest
}

package_python-sqlalchemy() {
  pkgdesc='Python SQL toolkit and Object Relational Mapper'
  depends=('python')
  optdepends=('python-psycopg2: connect to PostgreSQL database')

  cd SQLAlchemy-$pkgver
  python setup.py install --root="${pkgdir}"
  install -D -m644 LICENSE \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-sqlalchemy() {
  pkgdesc='Python 2 SQL toolkit and Object Relational Mapper'
  depends=('python2')
  optdepends=('python2-psycopg2: connect to PostgreSQL database')

  cd SQLAlchemy2-$pkgver
  python2 setup.py install --root="$pkgdir"
  install -D -m644 LICENSE \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 ft=sh et:
