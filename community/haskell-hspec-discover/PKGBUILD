# $Id: PKGBUILD 361223 2018-07-18 02:06:11Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec-discover
pkgname=haskell-hspec-discover
pkgver=2.5.5
pkgrel=1
pkgdesc="Automatically discover and run Hspec tests"
url="http://hspec.github.io/"
license=('MIT')
arch=('x86_64' 'ppc' 'ppc64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-hspec-meta')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('922511709402a063eba16f2bb2b85e42b6183d8004d78af00ddf692cfcc6c3172c9ce925f395c28b04bbe2289693c437923bd769f0289e00c589890e4475700e')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
