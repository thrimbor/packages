# $Id: PKGBUILD 339676 2018-06-04 02:08:32Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ChasingBottoms
pkgname=haskell-chasingbottoms
pkgver=1.3.1.4
pkgrel=5
pkgdesc="For testing partial and infinite values"
url="https://hackage.haskell.org/package/ChasingBottoms"
license=('MIT')
arch=('x86_64' 'ppc' 'ppc64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-random' 'haskell-syb')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('3d6814968c93ec35bd15a2959012e433a524ac2575de2cf1a2803c48409c320f19500c612c96db39eb02c99d3e45a634cb87abf09e40a79583f07bd2e7b510dd')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENCE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENCE"
}
