# $Id: PKGBUILD 364525 2018-07-29 14:52:51Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=attoparsec
pkgname=haskell-attoparsec
pkgver=0.13.2.2
pkgrel=25
pkgdesc="Fast combinator parsing for bytestrings and text"
url="https://github.com/bos/attoparsec"
license=("BSD3")
arch=('x86_64' 'ppc' 'ppc64')
depends=('ghc-libs' 'haskell-scientific')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-quickcheck-unicode' 'haskell-tasty'
             'haskell-tasty-quickcheck' 'haskell-vector')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('3726b9d3c45289b2148c8df99d41a31fbb32a9783e1ef3940110bf064da0d000277cba54a3d990210e48a4fcaa8ed016228be7d26861e74de40c971bd7c89ea6')

prepare() {
    sed -i 's/QuickCheck >= 2.7 && < 2.10,/QuickCheck >= 2.7,/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-developer
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Tests failed, possibly due to unmet dependency version on QuickCheck"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
