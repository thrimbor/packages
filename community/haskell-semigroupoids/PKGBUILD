# $Id: PKGBUILD 372355 2018-08-17 10:00:36Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=semigroupoids
pkgname=haskell-semigroupoids
pkgver=5.3.1
pkgrel=12
pkgdesc="Semigroupoids: Category sans id"
url="https://github.com/ekmett/semigroupoids"
license=("custom:BSD3")
arch=('x86_64' 'ppc' 'ppc64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-bifunctors' 'haskell-comonad' 'haskell-contravariant'
         'haskell-distributive' 'haskell-hashable' 'haskell-tagged' 'haskell-transformers-compat'
         'haskell-unordered-containers')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('8170f9f92a5f61325eb821d299e464106e08db3a8bac371fd427f329e479a4e914ed961a300760049aeff41fef255193dd93dfb752af0cc0027481f0ca6e6e35')

prepare() {
    cd $_hkgname-$pkgver
    sed -e 's/<.*0.14/<1/' -e '/semigroups/d' -e 's/<.*0.6/<1/' -i $_hkgname.cabal
    sed -i 's/#if MIN_VERSION_semigroups.*/#if 1/' src/Data/Semigroup/Traversable/Class.hs src/Data/Semigroup/Foldable/Class.hs
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -ftagged -fcomonad -fdoctests -fdistributive -fcontravariant -fcontainers -funordered-containers
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
