# $Id: PKGBUILD 369556 2018-08-06 08:16:20Z foutrelis $
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: xantares

pkgbase=python-sphinx-alabaster-theme
pkgname=(python-sphinx-alabaster-theme python2-sphinx-alabaster-theme)
pkgver=0.7.11
pkgrel=2

pkgdesc="Sphinx default theme"
url='https://github.com/bitprophet/alabaster'
arch=('any')
license=('custom:BSD')

makedepends=('python-setuptools' 'python2-setuptools')

source=("https://pypi.org/packages/source/a/alabaster/alabaster-$pkgver.tar.gz")

md5sums=('a23493869f819326a7f62deb7dbdb849')

build() {
	cp -r alabaster-"$pkgver" alabaster-"$pkgver"-py2

	cd alabaster-"$pkgver"
	python setup.py build

	cd "$srcdir"/alabaster-"$pkgver"-py2
	python2 setup.py build
}

package_python-sphinx-alabaster-theme() {
	cd alabaster-"$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1

	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

package_python2-sphinx-alabaster-theme() {
	cd alabaster-"$pkgver"-py2
	python2 setup.py install --root="$pkgdir" --optimize=1

	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
