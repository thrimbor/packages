# $Id: PKGBUILD 364545 2018-07-29 14:53:07Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec
pkgname=haskell-hspec
pkgver=2.5.5
pkgrel=2
pkgdesc="A Testing Framework for Haskell"
url="http://hspec.github.io/"
license=('MIT')
arch=('x86_64' 'ppc' 'ppc64')
depends=('ghc-libs' 'haskell-hunit' 'haskell-quickcheck' 'haskell-call-stack' 'haskell-hspec-core'
         'haskell-hspec-discover' 'haskell-hspec-expectations')
makedepends=('ghc')
checkdepends=('haskell-hspec-meta' 'haskell-stringbuilder')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('b15a9cb1eb85b7c6f6b28bce264d80e6019a650c40baf835c85bce610060534580ec73ae725fb6562a30a8a56d7b10a40a8db7cdbba5893ebfb446b1cb13e203')

build() {
    cd $_hkgname-$pkgver

    if (( CHECKFUNC )); then
        _opts=('--enable-tests')
    else
        _opts=('--disable-tests')
    fi
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" "${_opts[@]}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
