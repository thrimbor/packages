# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com

pkgbase=python-sphinxcontrib-websupport
pkgname=(python-sphinxcontrib-websupport python2-sphinxcontrib-websupport)
pkgver=1.1.0
pkgrel=2
pkgdesc="Sphinx API for Web Apps"
arch=('any')
url="http://www.sphinx-doc.org/"
license=('BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=(https://pypi.org/packages/source/s/sphinxcontrib-websupport/sphinxcontrib-websupport-$pkgver.tar.gz)
sha512sums=('adbd7db06150a4424a881a22442c2535ed823c7adcc295ef9c3af5ae38d823349830a114e8a7af2138498d0e68b37189707c1bb5a28b45c76031bcf94210fb89')

prepare() {
  cp -a sphinxcontrib-websupport-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver
  python setup.py build

  cd "$srcdir"/sphinxcontrib-websupport-$pkgver-py2
  python2 setup.py build
}

package_python-sphinxcontrib-websupport() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sphinxcontrib-websupport() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
