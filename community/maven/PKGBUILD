# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=maven
pkgver=3.5.2
pkgrel=1
pkgdesc='Java project management and project comprehension tool'
url='https://maven.apache.org'
arch=('any')
license=('APACHE')
depends=('java-environment' 'bash')
makedepends=('maven')
backup=('opt/maven/conf/settings.xml')
source=(https://www-eu.apache.org/dist/maven/maven-3/${pkgver}/source/apache-maven-${pkgver}-src.tar.gz{,.asc}
        maven.sh)
validpgpkeys=('042B29E928995B9DB963C636C7CA19B7B620D787') # StephenConnolly<stephenc@apache.org>
sha256sums=('246653dd9920f87b54c67ff4651b32d78ac4d7da973c10a6c8b1c70d9e6d0305'
            'SKIP'
            '24dc65607ff9e25c74ff2f75711911471910eaf415bce1856789c9d047209b8b')

package() {
  cd apache-maven-${pkgver}

  . /etc/profile.d/jre.sh

  install -d "${pkgdir}/opt/maven"
  mvn package \
    -Dmaven.repo.local="${srcdir}/repo" \
    -DdistributionTargetDir="${pkgdir}/opt/maven" \
    -Dproject.build.sourceEncoding=UTF-8

  install -Dm 755 ../maven.sh -t "${pkgdir}/etc/profile.d"
  install -d "${pkgdir}/usr/bin"
  ln -s /opt/maven/bin/mvn "${pkgdir}/usr/bin/mvn"
  ln -s /opt/maven/bin/mvnDebug "${pkgdir}/usr/bin/mvnDebug"
}

# vim: ts=2 sw=2 et:
