# $Id: PKGBUILD 339649 2018-06-04 02:08:03Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=integer-logarithms
pkgname=haskell-integer-logarithms 
pkgver=1.0.2.1
pkgrel=3
pkgdesc="Integer logarithms"
url="https://github.com/phadej/integer-logarithms"
license=('MIT')
arch=('x86_64' 'ppc' 'ppc64')
depends=('ghc-libs')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2f19593609e579f83b7aac1ae310aa537c525d1ed1eeb0dbe982a1c3d62e699a68828eb7e8838a4ad5ab6f77e77463ee719b70483154b873f3ad76da7cbbc504')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -finteger-gmp -f-check-bounds
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
