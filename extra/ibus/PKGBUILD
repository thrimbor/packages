# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=ibus
pkgname=(ibus libibus)
pkgver=1.5.18
pkgrel=2
pkgdesc="Next Generation Input Bus for Linux"
arch=('x86_64' 'ppc' 'ppc64')
url="https://github.com/ibus/ibus/wiki"
license=('LGPL')
depends=('dconf' 'gtk2' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'python-dbus' 'python-gobject'
         'iso-codes' 'python2-gobject2' 'python2-dbus' 'python2-gobject' 'librsvg')
makedepends=('gobject-introspection' 'vala' 'intltool' 'gnome-common' 'gtk-doc' 'gconf' 'qt5-base'
             'cldr-emoji-annotation' 'unicode-character-database' 'unicode-emoji')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/ibus/ibus/archive/$pkgver.tar.gz"
        https://github.com/ibus/ibus/commit/5788be80685f397c3db3bdf4e672d67cfb9b3433.patch)
sha512sums=('2548d3434b664d18c415d494e6e10f07c3911ce1c0c15a96125ec3eee3ff47b7e29bb44d1bbe6df71a8f49ee44ec8dee029813c49a0a1364f012732127bbf4be'
            '96244e1f3e7a5ec8bf6b3079a6fe37d3edcc77c7ab8e9c4db95978b0031faff0f1ea3457f54949cfb6e90a8ad2d7ef4abed5d8578d8b54a91f1ae39fa39e1f47')

prepare() {
  cd ibus-$pkgver
  patch -p1 -i ../5788be80685f397c3db3bdf4e672d67cfb9b3433.patch

  sed -i 's|$(libibus) $(libibus_emoji_dialog)|$(libibus_emoji_dialog) $(libibus)|' ui/gtk3/Makefile.am
}

build() {
  cd ibus-$pkgver
  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --disable-gconf \
    --enable-dconf \
    --enable-wayland \
    --enable-gtk-doc \
    --disable-memconf \
    --enable-ui \
    --enable-python-library \
    --with-python=python3 \
    --with-ucd-dir=/usr/share/unicode/
  make
}

package_ibus() {
  depends+=("libibus=$pkgver")
  install=ibus.install

  cd ibus-$pkgver
  make DESTDIR="$pkgdir" install
  make -C src DESTDIR="$pkgdir" uninstall
  make -C bindings DESTDIR="$pkgdir" uninstall
  rm -r "$pkgdir"/usr/lib/python2.7/site-packages/gi # compiled pyc/pyo files were not cleaned
  make DESTDIR="$pkgdir" uninstall-pkgconfigDATA
}

package_libibus() {
  pkgdesc="IBus support library"
  depends=('glib2')

  cd ibus-$pkgver
  make -C src DESTDIR="$pkgdir" install
  make -C bindings DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install-pkgconfigDATA
}
