# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=dconf
pkgver=0.28.0
pkgrel=1
pkgdesc="Configuration database system"
url="https://wiki.gnome.org/Projects/dconf"
arch=(x86_64 ppc ppc64)
license=(LGPL2.1)
depends=(glib2)
makedepends=(vala dbus git gtk-doc python meson)
_commit=fd884c24cf7d9631836c106ed4a895f18d707120  # tags/0.28.0^0
source=("git+https://git.gnome.org/browse/dconf#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D enable-gtk-doc=true
  ninja -C build
}

check() {
  cd build
  # dbus tests can't connect and dbus-run-session doesn't help
  meson test || :
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
