# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=mm-common
pkgver=0.9.12
pkgrel=1
pkgdesc="Common build files of the C++ bindings"
url="http://www.gtkmm.org/"
arch=(any)
depends=(graphviz doxygen gtk-doc libsigc++-docs perl-xml-parser)
makedepends=(libsigc++ git)
license=(GPL2)
_commit=9879643187642c20a59be650de00b441653d389c  # master
source=("git+https://git.gnome.org/browse/mm-common#commit=$_commit"
        https://gcc.gnu.org/onlinedocs/libstdc++/latest-doxygen/libstdc++.tag)
sha256sums=('SKIP'
            'f6c8c74e8293aefaefb4d25fd154d5f537b3bc80e7ceecaa02c5a01836fc09e6')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f 0.9.12 9879643187642c20a59be650de00b441653d389c
  cp ../libstdc++.tag doctags
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-network
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
