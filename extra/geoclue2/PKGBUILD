# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=geoclue2
pkgver=2.4.11
pkgrel=1
pkgdesc="Modular geoinformation service built on the D-Bus messaging system"
arch=(x86_64 'ppc' 'ppc64')
url="https://www.freedesktop.org/wiki/Software/GeoClue/"
license=(LGPL)
depends=(libsoup json-glib libmm-glib avahi libnotify)
makedepends=(intltool python2 systemd gobject-introspection gnome-common git vala)
backup=(etc/geoclue/geoclue.conf)
_commit=69a5d624c636f4e1fd6471db0d55b1100a0c4e65  # tags/2.4.11^0
source=("git+https://anongit.freedesktop.org/git/geoclue#commit=$_commit")
sha256sums=('SKIP')

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact foutrelis@archlinux.org for
# more information.
_google_api_key=AIzaSyDwr302FpOSkGRpLlUpPThNTDPbXcIn_FM

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact heftig@archlinux.org for
# more information.
_mozilla_api_key=16674381-f021-49de-8622-3021c5942aff

pkgver() {
  cd geoclue
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd geoclue
  sed -e "s/key=geoclue/key=$_mozilla_api_key/" \
      -e "s/key=YOUR_KEY/key=$_google_api_key/" \
      -i data/geoclue.conf.in
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd geoclue
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static --enable-gtk-doc --enable-vala
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd geoclue
  make DESTDIR="$pkgdir" install
}
