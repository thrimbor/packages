# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=js52
pkgver=52.9.0
pkgrel=1
_ffver=${pkgver%%.*}
pkgdesc="JavaScript interpreter and libraries - Version $_ffver"
arch=(x86_64 'ppc' 'ppc64')
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/SpiderMonkey"
license=(MPL)
depends=(gcc-libs readline zlib sh)
makedepends=(python2 zip autoconf2.13 mercurial)
_repo=https://hg.mozilla.org/mozilla-unified
source=("hg+$_repo#tag=FIREFOX_${pkgver//./_}esr_RELEASE"
        mozjs52-copy-headers.patch
        mozjs52-disable-mozglue.patch
        mozjs52-fix-soname.patch
        mozjs52-include-configure-script.patch)
sha256sums=('SKIP'
            'c5b3a88e4d10ef149aba6fc48d431db54ff266323fa22a5d549028fd794423cc'
            '5a84f02521f37de873991dd360a4c4bfdbdd2fb4a218e11be73f9cbbf02050e8'
            '728456fd9e66b69d6e0688c75e50091dc56735004ecf5f649212a83fe3087df1'
            'd91a89acd88bfc747a255050757a0c17139bf5c3508c2e1c3c6bb2056786a344')

prepare() {
  cd mozilla-unified

  # https://anonscm.debian.org/git/pkg-gnome/mozjs52.git/tree/debian/patches
  patch -Np1 -i ../mozjs52-fix-soname.patch
  patch -Np1 -i ../mozjs52-copy-headers.patch
  patch -Np1 -i ../mozjs52-disable-mozglue.patch
  patch -Np1 -i ../mozjs52-include-configure-script.patch
}

build() {
  unset CPPFLAGS
  CFLAGS+=' -fno-delete-null-pointer-checks -fno-strict-aliasing -fno-tree-vrp -flto=3'
  CXXFLAGS+=' -fno-delete-null-pointer-checks -fno-strict-aliasing -fno-tree-vrp -flto=3'
  export CC=gcc CXX=g++ PYTHON=/usr/bin/python2

  cd mozilla-unified/js/src
  sh configure \
    --prefix=/usr \
    --disable-debug \
    --disable-debug-symbols \
    --disable-strip \
    --enable-gold \
    --enable-optimize="-O2" \
    --enable-pie \
    --enable-posix-nspr-emulation \
    --enable-readline \
    --enable-release \
    --enable-shared-js \
    --enable-tests \
    --with-intl-api \
    --with-system-zlib \
    --without-system-icu
  make
}

check() {
  cd mozilla-unified/js/src
  python2 tests/jstests.py -d -s -t 300 --no-progress ../../js/src/js/src/shell/js
  python2 jit-test/jit_test.py -s -t 300 --no-progress ../../js/src/js/src/shell/js basic
}

package() {
  cd mozilla-unified/js/src
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/*.ajs
  find "$pkgdir"/usr/{lib/pkgconfig,include} -type f -exec chmod -c a-x {} +
}

# vim:set ts=2 sw=2 et:
