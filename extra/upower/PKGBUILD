# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=0.99.7
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('x86_64' 'ppc' 'ppc64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('systemd' 'libusb' 'libimobiledevice' 'libgudev')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection' 'python2' 'git' 'gtk-doc')
backup=('etc/UPower/UPower.conf')
_commit=b443f750cfeee460323d8d9f214ff691345d01ef  # tags/UPOWER_0_99_7^0
source=(git://anongit.freedesktop.org/upower#commit=$_commit)
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/UPOWER_//' -e 's/_/\./g' -e 's/-/+/g'
}

build() {
  cd $pkgname

  NOCONFIGURE=1 ./autogen.sh

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
