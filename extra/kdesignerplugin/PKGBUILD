# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesignerplugin
pkgver=5.48.0
pkgrel=1
pkgdesc='Integration of Frameworks widgets in Qt Designer/Creator'
arch=(x86_64 'ppc' 'ppc64')
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kplotting kio)
makedepends=(extra-cmake-modules qt5-tools kdoctools kdewebkit)
optdepends=('kdewebkit: KDEWebKit plugin')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5450307bb9d0bc369022fd6f7519e419628f5c2bbd7cf266b821fa2b5fdf5ccf'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
