# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gcr
pkgver=3.28.0
pkgrel=3
pkgdesc="A library for bits of crypto UI and parsing"
url="https://git.gnome.org/browse/gcr"
arch=(x86_64 ppc ppc64)
license=(GPL2)
depends=(dconf gtk3 libgcrypt p11-kit)
makedepends=(intltool gobject-introspection python vala libxslt git gtk-doc)
checkdepends=(python2)
_commit=aa4e0535a6f8e6bd6834a630ba2ea7b8990e402c  # tags/3.28.0^0
source=("git+https://git.gnome.org/browse/gcr#commit=$_commit"
        0001-Allow-D-Bus-to-launch-the-prompter-using-systemd.patch
        10-gcr.conf)
sha256sums=('SKIP'
            '464b395618dfbbb827a812de56deb3ca22ceb99010885c8f43dae22cfd66278d'
            '5f2eda7175ae9f23ee0e09d2beceb24fd2f6daafd7bddfcc1c1f5a3734eb60fc')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # https://bugzilla.gnome.org/show_bug.cgi?id=795626
  patch -Np1 -i ../0001-Allow-D-Bus-to-launch-the-prompter-using-systemd.patch

  sed -i '1s/python$/&2/' build/tap-{driver,gtester}
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --enable-gtk-doc \
    --disable-static \
    --disable-update-mime \
    --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  # Secure memory tests fail
  dbus-run-session make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  # gcr wants to lock some memory to prevent swapping out private keys
  # https://bugs.archlinux.org/task/32616
  # https://bugzilla.gnome.org/show_bug.cgi?id=688161
  install -Dt "$pkgdir/etc/security/limits.d" -m644 ../10-gcr.conf
}

# vim:set ts=2 sw=2 et:
