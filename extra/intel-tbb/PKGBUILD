# $Id$
# Maintainer:
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>

pkgname=intel-tbb
pkgver=2018_20180618
_pkgver=2018_U5
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(x86_64 'ppc' 'ppc64')
url='http://www.threadingbuildingblocks.org/'
license=(GPL)
depends=(gcc-libs)
source=(https://github.com/01org/tbb/archive/$_pkgver.tar.gz)
sha256sums=('c4c2896af527392496c5e01ef8579058a71b6eebbd695924cd138841c13f07be')

build() {
  cd tbb-$_pkgver
  if test "$CARCH" == ppc; then
    make arch=ppc32
  else
    make
  fi
}

package() {
  cd tbb-$_pkgver
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
}
