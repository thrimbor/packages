# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Patryk Kowalczyk <patryk@kowalczyk.ws>
pkgname=libiscsi
pkgver=1.17.0
pkgrel=2
makedepends=('git')
depends=('glibc' 'popt' 'libgcrypt')
pkgdesc="Clientside library to implement the iSCSI protocol"
arch=(i686 x86_64 'ppc' 'ppc64')
url="https://github.com/sahlberg/libiscsi"
license=('LGPL')
source=(libiscsi::git+https://github.com/sahlberg/libiscsi.git#tag=${pkgver})
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}"
  ./autogen.sh
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib --disable-werror
  make
}

check() {
  cd "${srcdir}/${pkgname}"
  make check
}

package () {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}
